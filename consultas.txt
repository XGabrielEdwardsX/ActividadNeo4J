// CONSULTAS
// 1) Consultar el historial de los contenidos multimedia visualizados por un usuario. Mostrar título, director, país de origen y fecha de visualización
MATCH (u:Usuario {idUsuario: 1})-[m:MIRA]->(c)
OPTIONAL MATCH (c)-[:ES_PARTE_DE]->(:Temporada)<-[:TIENE_UNA]-(s:Serie)-[:TIENE_ORIGEN]->(pS)
OPTIONAL MATCH (c)-[:TIENE_ORIGEN]->(pC:Pais)
OPTIONAL MATCH (c)-[:DIRIGIDA_POR]->(d:Director)
WITH u, c, d, pC, pS, m
RETURN 
    u.nombre AS usuario,
    c.titulo AS titulo,
    d.nombre AS director,
    COALESCE(pS.nombre, pC.nombre) AS pais,
    m.fechaVisualizacion AS fechaVisualizacion
;

// 2) ¿Cuáles son los contenidos multimedia más demandados por los clientes de un sexo (hombre/mujer)?
MATCH (u:Usuario {sexo: 'H'})-[:MIRA]->(c)
WITH c, count(u) AS vistas
RETURN 
    c.titulo AS Contenido, 
    vistas
ORDER BY vistas DESC
;

//3) ¿Cuáles son los principales contenidos multimedia visualizados por los clientes con un origen cualquiera y rango de edad?
MATCH (u:Usuario)-[:MIRA]->(c)
WHERE u.edad >= 20 AND u.edad <= 25
WITH c, count(u) AS Vistas
RETURN 
    c.titulo AS Contenido,
    Vistas
ORDER BY Vistas DESC
;

//4)¿Cuál es el contenido multimedia más visualizado? Mostrar el título, país de origen, fecha de lanzamiento y número de visualizaciones
MATCH (u)-[:MIRA]->(c)
OPTIONAL MATCH (c)-[:ES_PARTE_DE]->(t:Temporada)<-[:TIENE_UNA]-(s:Serie)-[:TIENE_ORIGEN]->(pS)
OPTIONAL MATCH (c)-[:TIENE_ORIGEN]->(pC:Pais)
WITH count(u) AS Vistas, c, t, pC, pS
RETURN 
    c.titulo AS Titulo,
    COALESCE(pS.nombre, pC.nombre) AS pais,
    COALESCE(c.anioLanzamiento, t.anioLanzamiento) AS FechaLanzamiento,
    Vistas
ORDER BY Vistas DESC
;

//5)Sugerir contenido similar respecto a un género multimedia visualizado por el usuario previamente
MATCH (u:Usuario {idUsuario: 50})-[:MIRA]->(c)
MATCH (c2)-[:PERTENECE_A]->(g:Genero)
WHERE (c:Pelicula AND (c)-[:PERTENECE_A]->(g))
    OR (c:Episodio AND (c)-[:ES_PARTE_DE]->(:Temporada)<-[:TIENE_UNA]-(:Serie)-[:PERTENECE_A]->(g))
    AND NOT (u)-[:MIRA]->(c2)
    AND NOT c = c2
    AND (c2:Pelicula OR c2:Episodio)
RETURN 
    u.nombre AS Usuario,
    c2.titulo AS Contenido,
    g.nombre AS Género
;

//6)Sugerir contenido basado en un tipo de reacción como puede ser 'Me gusta' o 'Me encanta' respecto a otros usuarios que sean del mismo país
MATCH (u1:Usuario {idUsuario: 2})-[:TIENE_NACIONALIDAD]->(p:Pais)<-[:TIENE_NACIONALIDAD]-(u2:Usuario)
WHERE u1 <> u2
MATCH (u2)-[r:REACCIONA_A]->(c)
OPTIONAL MATCH (c)-[:ES_PARTE_DE]->(t:Temporada)<-[:TIENE_UNA]-(s:Serie)
OPTIONAL MATCH (c)-[:TIENE_ORIGEN]->(pC:Pais)
WHERE r.tipoReaccion IN ["Me gusta", "Me encanta"] 
    AND NOT (u1)-[:MIRA]->(c) 
RETURN DISTINCT
    u1.nombre AS usuario, 
    COALESCE(s.titulo, c.titulo) AS Recomendación,
    COALESCE(t.anioLanzamiento, c.anioLanzamiento) AS Anio
ORDER BY Recomendación, Anio
;

